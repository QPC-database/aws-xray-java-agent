apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    compile (
        // Lambda Function Dependencies
        "com.amazonaws:aws-lambda-java-core:1.2.0",
        "com.amazonaws:aws-lambda-java-events:2.2.6",
        "com.amazonaws:aws-java-sdk-dynamodb:1.11.228",
        "com.amazonaws:aws-java-sdk-lambda:1.11.228",
        "com.amazonaws:aws-java-sdk-sqs:1.11.228",
        "com.amazonaws:aws-java-sdk-s3:1.11.228",

        // Agent dependencies
        "com.amazonaws:aws-xray-recorder-sdk-core:2.4.0",
        "com.amazonaws:aws-xray-auto-instrumentation-agent-installer:2.4.0-beta",
        "com.amazonaws:aws-xray-auto-instrumentation-agent-runtime:2.4.0-beta",
        "com.amazonaws:aws-xray-auto-instrumentation-agent-bootstrap:2.4.0-beta",
    )
}

task buildSourceZip(type: Zip) {
    // Bundle the compiled function code itself
    archiveFileName = "XRayJavaAgentLambda1Source.zip"
    from compileJava
}

task buildFunctionLayerZip(type: Zip) {
    archiveFileName = "XRayJavaAgentLambda1Dep.zip"
    into('java') {
        into('lib') {
            // Add all the dependencies except the agent jars.
            from configurations.runtime.copyRecursive()
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-installer')
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-bootstrap')
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-runtime')
        }
    }
}

task buildAgentLayerZip(type: Zip) {
    archiveFileName = "XRayJavaAgent.zip"
    into('java') {
        into('lib') {
            // Needed for bundling agent.
            from configurations.runtime.copyRecursive().findAll {
                it.name.startsWith('aws-xray-auto-instrumentation-agent')
            }

            // Needed for tools.jar
            from processResources
        }
    }
}

build.dependsOn buildSourceZip, buildFunctionLayerZip, buildAgentLayerZip // Add this when dependencies change.
apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    compile (
        // Lambda Function Dependencies
        "com.amazonaws:aws-lambda-java-core:1.2.0",
        "com.amazonaws:aws-lambda-java-events:2.2.6",
        "com.amazonaws:aws-java-sdk-dynamodb:1.11.398",
        "com.amazonaws:aws-java-sdk-lambda:1.11.398",
        "com.amazonaws:aws-java-sdk-sqs:1.11.398",
        "com.amazonaws:aws-java-sdk-s3:1.11.398",

        // Agent dependencies
        "com.amazonaws:aws-xray-auto-instrumentation-agent-installer:2.4.0-beta.1",
        "com.amazonaws:aws-xray-auto-instrumentation-agent-runtime:2.4.0-beta.1",
        "com.amazonaws:aws-xray-auto-instrumentation-agent-bootstrap:2.4.0-beta.1",
    )
}

task buildSourceZip(type: Zip) {
    // Bundle the compiled function code itself
    archiveFileName = "XRayJavaAgentLambda1Source.zip"
    from compileJava
}

task buildFunctionLayerZip(type: Zip) {
    archiveFileName = "XRayJavaAgentLambda1Dep.zip"
    into('java') {
        into('lib') {
            // Add all the dependencies except the agent jars.
            // We exclude the agent packages to separate out the dependencies the function itself
            // makes, and the dependencies the agent makes.
            from configurations.runtime.copyRecursive()
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-installer')
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-bootstrap')
                    .exclude(group: 'com.amazonaws', module:'aws-xray-auto-instrumentation-agent-runtime')
        }
    }
}

task buildAgentLayerZip(type: Zip) {
    archiveFileName = "XRayJavaAgent.zip"
    into('java') {
        into('lib') {
            // Needed for bundling the agent and its transitive dependencies, including the AWS X-Ray SDK
            from configurations.compile.resolvedConfiguration.getFiles {
                it.name == "aws-xray-auto-instrumentation-agent-bootstrap" ||
                it.name == "aws-xray-auto-instrumentation-agent-runtime" ||
                it.name == "aws-xray-auto-instrumentation-agent-installer"
            }

            // Needed for tools.jar
            from processResources
        }
    }
}

build.dependsOn buildSourceZip, buildFunctionLayerZip, buildAgentLayerZip // Add this when dependencies change.